ext {
    javaVersion = JavaVersion.VERSION_17
    pac4jVersion = "6.0.3"
//    pac4jVersion = "4.5.7"
    springBootVersion = "3.1.12"
    shiroVersion = "1.9.0"
    togglzVersion = "3.2.1"
}

allprojects {
    group = "com.baomidou"
    version = "2.0.0-SNAPSHOT"

    repositories {
        mavenLocal()
//        maven { url "https://maven.aliyun.com/nexus/content/groups/public/" }
        mavenCentral()
//        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
}

description = "基于 pac4j-jwt 的 WEB 安全组件"

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'com.github.hierynomus.license'

    java {
        sourceCompatibility = "${javaVersion}"
        targetCompatibility = "${javaVersion}"
    }

    lombok {
        version = "1.18.32"
    }

    dependencies {
        implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
        annotationProcessor(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))

        testImplementation("org.assertj:assertj-core")
        testImplementation("org.junit.jupiter:junit-jupiter-engine")
        testImplementation("org.mockito:mockito-junit-jupiter")
        testImplementation("ch.qos.logback:logback-classic")
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.warnings = false
        options.deprecation = true
        options.compilerArgs += ["-parameters"]
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    license {
        encoding = "UTF-8"
        header = rootProject.file("license.txt")
        include "**/*.java"
        exclude "**/test/**/*.java"
        exclude "**/*Test.java"
        mapping "java", "SLASHSTAR_STYLE"
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    }

    //noinspection GroovyAssignabilityCheck
    task sourcesJar(type: Jar) {
        archiveClassifier = "sources"
        from sourceSets.main.allJava
    }

    tasks.clean {
        delete "${projectDir}/build"
        delete "${projectDir}/out"
    }

    javadoc {
        afterEvaluate {
            configure(options) {
                encoding "UTF-8"
                charSet 'UTF-8'
                author true
                version true
                failOnError false
                links "http://docs.oracle.com/javase/8/docs/api"
            }
        }
    }

    task javadocJar(type: Jar) {
        archiveClassifier = "javadoc"
        from javadoc
    }

    test {
        useJUnitPlatform()
    }

    tasks.whenTaskAdded { task ->
        if (task.name.contains('signMavenJavaPublication')) {
            task.enabled = new File(project.property('signing.secretKeyRingFile') as String).isFile()
        }
    }
}